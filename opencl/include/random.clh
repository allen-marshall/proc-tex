#pragma once
#pragma OPENCL EXTENSION cl_khr_fp64 : enable

const uint MODULUS = 2147483647;
const uint MULTIPLIER = 16807;

void updateSeed(uint *seed) {
  if (seed) {
    if (*seed == 0) {
      *seed = 1;
    }
    *seed = (*seed * (ulong) MULTIPLIER) % MODULUS;
  }
}

uint initWorkerSeed(uint seedBase, uint workerID) {
  uint result = seedBase ^ workerID;
  // TODO: Replace this with something that has some theoretical justification,
  // if possible. I just put it here because the randomness doesn't seem to work
  // well without it.
  for (int i = 0; i < (((seedBase ^ workerID) * 10000) % 1009); i++) {
    updateSeed(&result);
  }
  return result;
}

/*
 * Generates an approximately uniform random double in the range [0, 1], and
 * updates the seed value for use in later random number calculations.
 */
double randDouble(uint *seed) {
  updateSeed(seed);
  return (*seed - 1) / (double) (MODULUS - 2);
}

/*
 * Generates an approximately uniform random double in the range
 * [minVal, maxVal], and updates the seed value for use in later random number
 * calculations.
 */
double randDoubleInRange(uint *seed, double minVal, double maxVal) {
  return randDouble(seed) * (maxVal - minVal) + minVal;
}

/*
 * Generates a random 2D vector with the specified magnitude, and updates the
 * seed value for use in later random number calculations.
 */
double2 randVecWithMagnitude2D(uint *seed, double magnitude) {
  // TODO: I think this function has a bit of a bias toward certain directions
  // over others. Ideally, the direction should be uniformly random.
  
  // Generate a random vector of non-zero length.
  double2 vec = (double2) (0, 0);
  double currLength = 0;
  while (currLength == 0) {
    vec.x = randDoubleInRange(seed, -1, 1);
    vec.y = randDoubleInRange(seed, -1, 1);
    currLength = length(vec);
  }
  
  // Scale to the required magnitude.
  return vec * magnitude / currLength;
}

/*
 * Generates a random 3D vector with the specified magnitude, and updates the
 * seed value for use in later random number calculations.
 */
double3 randVecWithMagnitude3D(uint *seed, double magnitude) {
  // TODO: I think this function has a bit of a bias toward certain directions
  // over others. Ideally, the direction should be uniformly random.
  
  // Generate a random vector of non-zero length.
  double3 vec = (double3) (0, 0, 0);
  double currLength = 0;
  while (currLength == 0) {
    vec.x = randDoubleInRange(seed, -1, 1);
    vec.y = randDoubleInRange(seed, -1, 1);
    vec.z = randDoubleInRange(seed, -1, 1);
    currLength = length(vec);
  }
  
  // Scale to the required magnitude.
  return vec * magnitude / currLength;
}
